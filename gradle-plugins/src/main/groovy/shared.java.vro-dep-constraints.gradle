/**
 * This plugin is used to specify versions of transitive dependencies to address security vulnerability alerts.
 * If more formality is desired, convert this into a Platform -- https://docs.gradle.org/current/userguide/platforms.html#sub:platforms-vs-catalog
 *   - This would result in creating another Gradle module/subfolder, which seems unnecessary at this point.
 */
plugins {
  id 'java'
}

ext {
    mapstruct_version="1.4.2.Final"
    spring_framework_version="5.3.29"
    snakeyaml_version="2.0"
}

afterEvaluate {
    configurations.all {
      resolutionStrategy.dependencySubstitution {
        substitute module("org.yaml:snakeyaml") using module("org.yaml:snakeyaml:${snakeyaml_version}")
      }
    }
}

dependencies {
    // The following constraints are declared to address security vulnerabilities in prior versions.
    // Add code comments to hint about uses of each dependency.
    constraints {
        // for starter-bom
        implementation "org.springframework.boot:spring-boot-dependencies:${spring_boot_version}"
        implementation "org.springframework:spring-webmvc:${spring_framework_version}"
        implementation "org.springframework:spring-beans:${spring_framework_version}"
        implementation "io.github.classgraph:classgraph:4.8.149"
        implementation "com.fasterxml.jackson.core:jackson-databind:2.13.4.2"
        implementation "com.squareup.okhttp3:okhttp:4.10.0"
        implementation "org.yaml:snakeyaml:${snakeyaml_version}"
        implementation "org.thymeleaf:thymeleaf:3.1.2.RELEASE"      // Fix for Security vulnerability

        // for mockserver-netty and hapi-fhir
        implementation 'org.apache.commons:commons-text:1.10.0'
        implementation 'org.apache.commons:commons-compress:1.24.0'

        // for hapi-fhir-server-openapi
        implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.utilities:5.6.92'
        implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.r5:5.6.92'
        implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.r4b:5.6.92'
        implementation 'ca.uhn.hapi.fhir:org.hl7.fhir.convertors:5.6.92'

        implementation('io.netty:netty-handler:4.1.94.Final')

        // For Kafka svc-bie-kafka service override
        implementation('org.xerial.snappy:snappy-java:1.1.10.4')

        implementation('com.google.guava:guava:32.0.1-jre')
        implementation 'org.bouncycastle:bcpkix-jdk18on:1.74'
        implementation 'org.webjars:swagger-ui:4.1.3'
        implementation 'org.springframework.kafka:spring-kafka:2.9.11'

        // These are used indirectly but we need to specify versions, otherwise build error
        annotationProcessor "org.springframework.boot:spring-boot-autoconfigure-processor:${spring_boot_version}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"
    }
}
