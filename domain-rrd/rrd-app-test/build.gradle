plugins {
  id 'local.std.java.application-conventions'
  id 'starter.java.config-conventions'
  id 'starter.java.build-utils-conventions'
  id 'org.unbroken-dome.test-sets' version '4.0.0'
}

bootJar {
  enabled = false
}

jar {
  enabled = true
}

testSets {
  end2endTest
}

tasks.withType(Test).named("end2endTest") {
  useJUnitPlatform()
  testLogging {
    // https://wjw465150.github.io/blog/Gradle/my_data/Gradle_Goodness/Show_Standard_Out_or_Error_Output_from_Tests.htm
    showStandardStreams = true
    events "skipped", "failed", "passed"
    exceptionFormat = 'full'
    showStackTraces = true
    showExceptions = true
    showCauses = true
  }
}

dependencies {
  implementation project(':app')
  implementation project(':persistence:model')
  implementation project(':domain-rrd:rrd-shared')
  implementation project(':domain-rrd:rrd-api-controller')
  implementation project(':domain-rrd:rrd-workflows')

  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation "org.springframework.security:spring-security-core:${spring_security_version}"
  implementation "org.springframework.security:spring-security-config:${spring_security_version}"
  implementation "org.springframework.security:spring-security-web:${spring_security_version}"

  //MAS oAuth2 webclient libs
  implementation "org.springframework.boot:spring-boot-starter-webflux:${sb_webflux}"
  implementation "org.springframework.security:spring-security-oauth2-client:${spring_security_version}"
  implementation "org.springframework.boot:spring-boot-starter-security:${sb_security}"

  runtimeOnly "org.postgresql:postgresql:${postgresql_version}"
  testRuntimeOnly "com.h2database:h2:${h2_version}"

  testImplementation "org.apache.camel:camel-test-spring-junit5:${camel_version}"

  // end to end dependencies
  end2endTestImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
  end2endTestImplementation "org.springframework.boot:spring-boot-starter-webflux:${spring_boot_version})"
  end2endTestImplementation "org.skyscreamer:jsonassert:${json_assert_version}"
  end2endTestImplementation "org.apache.pdfbox:pdfbox:2.0.28"
}

openApi {
  // Generates build/springdoc/openapi.json
  customBootRun {
    getClasspath().setFrom(sourceSets.test.runtimeClasspath)
    args = [
      "--spring.profiles.active=test"
    ]
  }
}

afterEvaluate {
  // Disable https://github.com/springdoc/springdoc-openapi-gradle-plugin
  // Not particularly useful
  generateOpenApiDocs.enabled = false
  forkedSpringBootRun.enabled = false

  tasks.named('forkedSpringBootRun').configure {
    dependsOn 'bootStartScripts'
    dependsOn 'checkstyleIntegrationTest'
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'compileIntegrationTestJava'
    dependsOn 'dockerfileZip'
    dependsOn 'integrationTest'
    dependsOn 'jar'
    dependsOn 'spotlessJava'
    dependsOn 'startScripts'
    dependsOn 'test'
    // dependsOn ':service-python:pytest' // slows down `:app:build` task

    dependsOn 'jacocoTestReport'
    dependsOn 'jacocoTestCoverageVerification'

    dependsOn 'dockerPrepare'
  }
}
