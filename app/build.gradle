plugins {
    id 'starter.std.java.application-conventions'
    id 'starter.java.config-conventions'
    id 'starter.java.build-utils-conventions'
}

dependencies {
    annotationBom platform("gov.va.starter:starter-bom:${starter_boot_version}")
    checkstyleRules platform("gov.va.starter:checkstyle-bom:${starter_boot_version}")

    implementation project(':api')
    implementation project(':service')
    implementation project(':test-data-factory')

    implementation 'gov.va.starter:health'
    implementation 'gov.va.starter:tracing'
    implementation 'gov.va.starter:error-handling'
    implementation 'gov.va.starter:exceptions'
    implementation 'gov.va.starter:open-api'
    implementation 'gov.va.starter:kafka-entity-lifecycle-notifier'
    implementation 'gov.va.starter:test-data'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'com.h2database:h2'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.3'
    testImplementation 'org.testcontainers:postgresql:1.17.3'
    //This version is compatible with current spring version: 5.3.8
    testImplementation 'org.apache.camel:camel-test-spring-junit5:3.11.0'

    docker project(':container-init')
    docker project(':db-init')
    docker project(':opa-init')
}


application {
    // Define the main class for the application.
    mainClass = 'gov.va.vro.VroApplication'
}

bootJar {
    manifest {
        attributes(
                'Application-Version': "${project.version}",
                'Start-Class': 'gov.va.vro.VroApplication',
                'Application-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        )
    }
}

repositories {
    mavenCentral()
}

afterEvaluate {
    tasks.getByName('spotlessCheck').dependsOn(tasks.getByName('spotlessApply'))
}

tasks.named('lintDockerfile').configure {
    ext.targets = ["src/docker/Dockerfile"]
}

tasks.named('dockerComposeUp').configure {
    dependsOn ':container-init:docker'
    dependsOn ':db-init:docker'
    dependsOn ':opa-init:docker'
}
