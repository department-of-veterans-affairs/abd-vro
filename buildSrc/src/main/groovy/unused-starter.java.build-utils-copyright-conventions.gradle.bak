/**
 * Tasks for maintaining copyright dates
 * - updateCopyrights  scans modified files for copyright string and updates to current year
 */

plugins {
}
// Requires
// id 'starter.java.build-utils-fileset-conventions'

tasks.register('updateCopyrights') {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Update the copyright dates for any files which have been modified"
    onlyIf { gitPresent && !System.getenv('GITHUB_ACTION') }
    if (gitPresent) {
        def extensions = [".java", ".kt"]
        inputs.files(filterProjectFiles(modifiedFiles, extensions))
    }
    outputs.dir('build')

    doLast {
        def now = Calendar.instance.get(Calendar.YEAR) as String
        inputs.files.each { file ->
            def line
            file.withReader { reader ->
                while (line = reader.readLine()) {
                    def matcher = line =~ /Copyright (20\d\d)-?(20\d\d)?/
                    if (matcher.count) {
                        def beginningYear = matcher[0][1]
                        if (now != beginningYear && now != matcher[0][2]) {
                            def years = "$beginningYear-$now"
                            def sourceCode = file.text
                            sourceCode = sourceCode.replaceFirst(/20\d\d(-20\d\d)?/, years)
                            file.write(sourceCode)
                            println "Copyright updated for file: $file"
                        }
                        break
                    }
                }
            }
        }
    }
}
