import java.text.SimpleDateFormat

plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id "io.freefair.lombok" version "6.5.0.2"
    id 'starter.java.container-conventions'
    id 'starter.java.build-utils-conventions'
    id 'java'
    id 'jacoco'

}


group = 'gov.va'
version = '0.0.1-SNAPSHOT'
description = 'Automated Benefit Delivery Data Access Service'
java.sourceCompatibility = JavaVersion.VERSION_17

tasks.dockerPrepare.configure {
    dependsOn(tasks.bootJar.name)
}

ext {
    springBootVersion = '2.6.7'
    camelVersion = '3.18.0'
    testcontainersVersion = '1.17.3'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework:spring-web:5.3.22'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.core:jackson-core' 
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-base:5.7.2'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-server-openapi:5.7.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.projectlombok:lombok:1.18.24'
    implementation "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'io.jsonwebtoken:jjwt:0.2'
    implementation 'com.auth0:java-jwt:2.2.2'
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.testcontainers:junit-jupiter:$testcontainersVersion"
    testImplementation "org.testcontainers:rabbitmq:$testcontainersVersion"
    testImplementation "org.apache.camel.springboot:camel-spring-boot-starter:$camelVersion"
    testImplementation "org.apache.camel.springboot:camel-rabbitmq-starter:$camelVersion"
    testImplementation "org.apache.camel.springboot:camel-jackson-starter:$camelVersion"
    testImplementation 'org.skyscreamer:jsonassert:1.2.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.7.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'


    integrationTestImplementation 'org.testcontainers:testcontainers:$testcontainersVersion'
    integrationTestImplementation 'org.testcontainers:junit-jupiter:$testcontainersVersion'
    integrationTestImplementation 'org.testcontainers:rabbitmq:$testcontainersVersion'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.7'
    integrationTestImplementation 'org.apache.camel.springboot:camel-spring-boot-starter:$camelVersion'
    integrationTestImplementation 'org.apache.camel.springboot:camel-rabbitmq-starter:$camelVersion'
    integrationTestImplementation 'org.apache.camel.springboot:camel-jackson-starter:$camelVersion'
    integrationTestImplementation 'org.skyscreamer:jsonassert:1.2.3'
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/jacoco")
}

test {
    useJUnitPlatform()
    exclude '**/integrationTest'
    finalizedBy jacocoTestReport {
        executionData(file("${project.buildDir}/jacoco/test.exec"))
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    finalizedBy jacocoTestReport {
        executionData(file("${project.buildDir}/jacoco/integrationTest.exec"))
    }
}

docker {
    files "build/libs/${bootJar.archiveFileName.get()}", "bin/entrypoint.sh"
    buildArgs([JAR_FILE: bootJar.archiveFileName.get(), ENTRYPOINT_FILE: "entrypoint.sh"])
}

bootJar {
    manifest {
        attributes(
                'Application-Version': "${project.version}",
                'Start-Class': 'gov.va.vro.abd_data_access.AbdApplication',
                'Application-Git-Commit': '${gitRevision}',
                'Application-Git-Branch-Name': '${gitBranchName}',
                'Application-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        )
    }
}
