plugins {
  id 'local.std.java.library-conventions'
  id 'local.java.container-spring-conventions'
  id "com.google.osdetector" version "1.7.3"
}

dependencies {
  // Spring Boot
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  // Spring Cloud
  implementation 'org.springframework.cloud:spring-cloud-starter-gateway:4.0.6'
  if (osdetector.classifier == "osx-aarch_64") {
    // Fix MacOS error: Unable to load io.netty.resolver.dns.macos.MacOSDnsServerAddressStreamProvider,
    // which may result in incorrect DNS resolutions.
    // Spring Cloud Gateway uses Netty
    runtimeOnly("io.netty:netty-resolver-dns-native-macos:4.1.92.Final:${osdetector.classifier}")
  }

  // Swagger UI for WebFlux
  implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.1.0'

  constraints {
    // To address Snyk alert on DoS vulnerability
    implementation 'org.springframework.boot:spring-boot-autoconfigure:3.1.2'
  }
}
